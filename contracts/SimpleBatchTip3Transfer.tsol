pragma ever-solidity 0.62.0;

pragma AbiHeader time;
pragma AbiHeader expire;
pragma AbiHeader pubkey;

import "@broxus/ever-contracts/contracts/access/abstract/Ownable.tsol";
import "@broxus/ever-contracts/contracts/libraries/UtilityGas.tsol";

import "@broxus/tip3/contracts/interfaces/ITokenRoot.tsol";
import "@broxus/tip3/contracts/interfaces/ITokenWallet.tsol";

contract SimpleBatchTip3Airdrop is Ownable {
    uint32 private static _nonce;

    address public tokenRoot;
    address public tokenWallet;

    modifier onlyTokenRoot() {
        require(tokenRoot.value != 0 && msg.sender == tokenRoot);
        _;
    }

    modifier onlyOwnerOrSelf() {
        require(msg.sender.value != 0 && (msg.sender == address(this) || msg.sender == _getOwnerInternal()));
        _;
    }

    function _getTargetBalanceInternal()
        internal
        view
        override
        returns (uint128)
    {
        return UtilityGas.INITIAL_BALANCE;
    }

    constructor(
        address _initialOwner,
        address _initialTokenRoot
    ) public reserveAndAccept(_getTargetBalanceInternal()) {
        _setOwnerInternal(_initialOwner);

        tokenRoot = _initialTokenRoot;

        ITokenRoot(_initialTokenRoot)
            .deployWallet{
                value: 0,
                flag: MsgFlag.ALL_NOT_RESERVED,
                callback: SimpleBatchTip3Airdrop.onTokenWallet
            }(address(this), 0.1 ever);
    }

    function onTokenWallet(address _tokenWallet)
        external
        onlyTokenRoot
        reserveAndRefund(
            _getTargetBalanceInternal(),
            _getOwnerInternal(),
            _getOwnerInternal()
        )
    {
        tokenWallet = _tokenWallet;
    }

    struct Request {
        address recipient;
        uint128 amount;
    }

    function batchTransfer(
        Request[] _requests,
        uint32 _offset,
        address _remainingGasTo
    )
        external
        view
        onlyOwnerOrSelf
        reserve(_getTargetBalanceInternal())
    {
        uint length = _requests.length;

        uint takeUntil = math.min(_offset + 10, length);

        for (uint i = _offset; i < takeUntil; i++) {
            _batchTransferInternal(_requests[i], _remainingGasTo);
        }

        if (takeUntil < length) {
            SimpleBatchTip3Airdrop(address(this))
                .batchTransfer{
                    value: 0,
                    flag: MsgFlag.ALL_NOT_RESERVED,
                    bounce: false
                }(_requests, uint32(takeUntil), _remainingGasTo);
        } else {
            _remainingGasTo.transfer({
                value: 0,
                flag: MsgFlag.ALL_NOT_RESERVED + MsgFlag.IGNORE_ERRORS,
                bounce: false
            });
        }
    }

    function _batchTransferInternal(Request _request, address _remainingGasTo) private view {
        TvmCell empty;

        ITokenWallet(tokenWallet)
            .transfer{
                value: 0.2 ever,
                flag: MsgFlag.SENDER_PAYS_FEES,
                bounce: true
            }(
                _request.amount,
                _request.recipient,
                0.1 ever,
                _remainingGasTo,
                false,
                empty
            );
    }

    function upgrade(
        TvmCell _code,
        address _remainingGasTo
    )
        external
        onlyOwner
        reserve(_getTargetBalanceInternal())
    {
        TvmCell data = abi.encode(
            _remainingGasTo,
            _getOwnerInternal(),
            tokenRoot,
            tokenWallet,
            _nonce
        );

        tvm.setcode(_code);
        tvm.setCurrentCode(_code);

        onCodeUpgrade(data);
    }

    function onCodeUpgrade(TvmCell _data) private {
        tvm.resetStorage();

        address remainingGasTo;
        address owner;

        (
            remainingGasTo,
            owner,
            tokenRoot,
            tokenWallet,
            _nonce
        ) = abi.decode(_data, (
            address,
            address,
            address,
            address,
            uint32
        ));

        _setOwnerSilent(owner);

        remainingGasTo.transfer({
            value: 0,
            flag: MsgFlag.ALL_NOT_RESERVED + MsgFlag.IGNORE_ERRORS,
            bounce: false
        });
    }
}
